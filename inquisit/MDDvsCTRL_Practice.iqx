2-Back Practice Task

Stim duration = 500
Inter-stim interval = 1500
Target range = 25%
Block = 5 targets, 15 other stim
Trial = 4 x blocks

Cues - 2, 3, 4, 6
	 - 3, 3, 4, 5

<data >
/ columns = [subject, blockcode, trialcode, trialnum, latency, response, stimulusitem, values.cueselecter, values.targets, values.cue1back, values.cue2back, values.cue3back, values.cue4back, values.cue5back, values.hits, values.hitsprop, script.groupid] 
</data>

<values>
/ cueselecter = 0
/ targets = 0
/ cue1back = 0
/ cue2back = 0
/ cue3back = 0
/ cue4back = 0
/ hits = 0
/ hitsprop = 0
</values>

<monkey >
/ percentcorrect = 75
/ latencydistribution = normal(500, 100)
</monkey>
**************************************************************************************************************************************************************************

<expt nback>
/ preinstructions =(page1, page2)
/ postinstructions =  (page3)
/ onblockend =  [values.hitsprop = values.hits/6*100]
/ blocks = [1=blank;2-7=noreplace(threecue,fourcue,fivecue)]
/ branch = [if (values.hitsprop < 50) expt.nback]
/ branch = [if (values.hitsprop >= 50) 0]
</expt>

<page page1>
^In this task you will see letters on the screen.
^^You need to remember the letter that was on the screen TWO letters ago.
^^If the letter on the screen now is the same as the letter that was on the screen TWO letters ago you need to press the SPACEBAR.
</page>

<page page2>
^e.g. the following letter sequence occurs:
^A, P, J, E, F, E, L, H, I, B, I, D, C, M, O, M,
^^It would be correct to press the SPACEBAR at the points marked with asterixis.
^A, P, J, E, F, E*, L, H, I, B, I*,D, C, M, O, M*
^^There is no break after you press the SPACEBAR. 
^When you press continue the task will begin.
</page>

<page page3>
^You selected the correct letter <%values.hitsprop%>% of the time.

^^Please wait for your experimenter.
</page>

*****************************************************************
<block blank>
/ trials =[1=blank]
</block>

<block threecue>
/ trials =[1-3=cue; 4=targets]
</block>

<block fourcue>
/ trials =[1-4=cue; 5=targets]
</block>

<block fivecue>
/ trials =[1-5=cue; 6=targets]
</block>

<block sixcue>
/ trials =[1-6=cue; 7=targets]
</block>

*****************************

<port targetsignal>
/ port = LPT1
/ subport = data
/ items = ("00000001")
/ erase = true("00000000")
</port>

<port cuesignal>
/ port = LPT1
/ subport = data
/ items = ("00000010")
/ erase = true("00000000")
</port>

<port responsesignal>
/ port = LPT1
/ subport = data
/ items = ("00000011")
/ erase = true("00000000")
</port>

*****************************

<trial cue>
/ ontrialbegin = [values.targets=counter.targets.selectedvalue; values.cueselecter=counter.cueselecter.selectedvalue]
/ ontrialend = [values.cue4back=values.cue3back; values.cue3back=values.cue2back; values.cue2back=values.cue1back; values.cue1back=values.cueselecter]
/ stimulustimes = [0 = blankscreen; 1=cue, cuesignal]
/ validresponse = (" ")
/ correctresponse = (noresponse)
/ responsemessage = (" ", cuer,0)
/ trialduration = 2000
/ responsetime = 30
/ recorddata = true
</trial>

<trial targets>
/ ontrialbegin = [values.targets=counter.targets.selectedvalue; values.cueselecter=counter.cueselecter.selectedvalue]
/ ontrialend = [values.cue4back=values.cue3back; values.cue3back=values.cue2back; values.cue2back=values.cue1back; values.cue1back=values.targets]
/ ontrialend = [if (trial.targets.correct) {values.hits = values.hits + 1}]
/ stimulustimes = [0 = blankscreen; 1=targets, targetsignal]
/ correctresponse = (" ")
/ responsemessage = (" ", cueg,0)
/ validresponse = (" ")
/ trialduration = 2000
/ responsetime = 30
/ recorddata = true
</trial>

<trial blank>
/ stimulustimes = [0=blankscreen]
/ ontrialbegin = [values.cue3back = 0; values.cue2back=0; values.cue1back=0; values.cueselecter=0;values.targets=0; values.hits=0]
/ validresponse = (noresponse)
/ trialduration = 2000
/ recorddata = false
</trial>

**************************************************************************************************************************************************************************
<text cue>
/ items = cues
/ select = values.cueselecter
/ fontstyle = ("Arial", 10%, true)
/ position = (50%,50%)
/ erase = false
</text>

<text cuer>
/ items = cues
/ select = values.cueselecter
/ txcolor = red
/ fontstyle = ("Arial", 10%, true)
/ position = (50%,50%)
/ erase = false
</text>

<text cueg>
/ items = cues
/ select = values.targets
/ txcolor = green
/ fontstyle = ("Arial", 10%, true)
/ position = (50%,50%)
/ erase = false
</text>

<item cues>
/ 1 = "A"
/ 2 = "B"
/ 3 = "C"
/ 4 = "D"
/ 5 = "E"
/ 6 = "F"
/ 7 = "G"
/ 8 = "H"
/ 9 = "I"
/ 10 = "J"
</item>

<counter cueselecter>
/ items = (1,2,3,4,5,6,7,8,9,10)
/ select = noreplace
/ not = (targets)
/ selectionrate = trial
</counter>


<text targets>
/ items = targets
/ select = values.targets
/ fontstyle = ("Arial", 10%, true)
/ position = (50%,50%)
/ erase = false
</text>

<item targets>
/ 1 = "A"
/ 2 = "B"
/ 3 = "C"
/ 4 = "D"
/ 5 = "E"
/ 6 = "F"
/ 7 = "G"
/ 8 = "H"
/ 9 = "I"
/ 10 = "J"
</item>

<counter targets>
/ items = (1,2,3,4,5,6,7,8,9,10)
/ select = values.cue2back
/ selectionrate = trial
</counter>


<text blankscreen>
/ items = ("     ")
/ size = (50%,50%)
/ position = (50%,50%)
/ erase = false
</text>